file = {
    SOI
    // IgotU, mGPS-2 - earth&Ocean Technologies
    ~ csv
    // Accelerometer
    | (
        "ACCELERATION DATA " ~ NEWLINE+
        ~ acceleration_header ~ NEWLINE+
        ~ csv
      )
    // Pathtrack
    | (
        "*"+ ~ NEWLINE
        ~ ((!NEWLINE ~ ANY)+ ~ NEWLINE){3}
        ~ "*"+ ~ NEWLINE
        ~ csv
      )
    // CatTrack
    | (
        section ~ NEWLINE+
        ~ "-"+ ~ NEWLINE
        ~ csv
      )
    // TDR
    | (
        (
            ((section ~ NEWLINE+){6} ~ datablock)
            | ((section ~ NEWLINE+){7} ~ datablock)
            | ((section ~ NEWLINE+){8} ~ datablock)
        )
        ~ csv ~ NEWLINE+
        ~ section ~ NEWLINE+
      )
    ~ EOI }

acceleration_header = _{
    " "* ~ frequency ~ " msec/point" ~ NEWLINE
    ~ "RECORD TIME" ~ (!NEWLINE ~ ANY)+ ~ NEWLINE
    ~ "START DATE" ~ " "+ ~ date ~ NEWLINE
    ~ "START TIME" ~ " "+ ~ time
}

frequency = { ASCII_DIGIT+ }
date = { ASCII_DIGIT{4} ~ ("/" ~ " "? ~ ASCII_DIGIT{1,2}){2} }
time = { ASCII_DIGIT{2} ~ (":" ~ ASCII_DIGIT{2}){2} }

// Multiple lines ending with an empty line
section = _{ ((!NEWLINE ~ ANY)+ ~ NEWLINE)+ }

// CSV parsing
csv = _{ (csv_header ~ NEWLINE) ~ (csv_line ~ NEWLINE)+ }
csv_sep = _{
    // CatTrack, IgotU
    ", "
    // Pathtrack, TDR, Accelerometer
    | ","
    //
    | ";"
    // mGPS-2 - earth&Ocean Technologies
    | "\t"
}
csv_line = {
    // mGPS-2 - earth&Ocean Technologies
    ((csv_cell ~ csv_sep)+ ~ "\t\t")
    // CatTrack, IgotU, Pathtrack, TDR
    | ((csv_cell ~ csv_sep)+ ~ csv_cell)
    // Accelerometer
    | ((csv_cell ~ csv_sep)+)
}
csv_header = { csv_line }
csv_cell = { (!(NEWLINE | csv_sep) ~ ANY)+ }

// TDR datablock
datablock = _{
    "Data Block " ~ ASCII_DIGIT ~ NEWLINE
     ~ (kv ~ NEWLINE)+
}
kv = _{ kv_cell ~ "=" ~ kv_cell }
kv_cell = _{ (!(NEWLINE | "=") ~ ANY)+ }
